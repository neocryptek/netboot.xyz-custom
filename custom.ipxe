#!ipxe
###
### netboot.xyz-custom menu
###

:custom
clear custom_choice
menu This is a Test Menu
item --gap This is the first sub menu
item ubcd ${space} Ultimate Boot CD (UBCD)
item ubcd_local ${space} Local Ultimate Boot CD (UBCD)
item option_one ${space} Loading a kernel and initrd
item option_two ${space} Loading an ISO
item --gap This is a second sub menu
item option_three ${space} Loads another custom sub menu
item option_four ${space} This is option four
choose custom_choice || goto custom_exit
echo ${cls}
goto ${custom_choice}
goto custom_exit

:ubcd
set util_path mirror.sysadminguide.net/ubcd/ubcd537.iso
set util_file ubcd537.iso
goto boot_memdisk

:boot_memdisk
imgfree
kernel ${memdisk} iso raw
initrd --name ${util_file} http://${util_path}
echo
echo MD5sums:
md5sum memdisk ${util_file}
iseq ${img_sigs_enabled} true && goto verify_sigs || goto skip_sigs
:verify_sigs
echo
echo Checking signatures...
imgverify memdisk ${sigs}memdisk.sig || goto error
imgverify ${util_file} ${sigs}${menu}/${util_file}.sig || goto error
echo Signatures verified!
echo
:skip_sigs
boot
goto custom_exit

:ubcd_local
set util_path file:/boot/images/ubcd.iso
set util_file ubcd.iso
goto boot_local_memdisk

:boot_local_memdisk
imgfree
kernel ${memdisk} iso raw
initrd --name ${util_file} ${util_path}
boot
goto custom_exit

:option_one
kernel http://path.to/vmlinuz
initrd http://path.to/initrd
imgargs vmlinuz put_kernel_img_args_here
boot || goto custom_exit

:option_two
kernel https://boot.netboot.xyz/memdisk raw iso
initrd http://path.to/iso
boot || goto custom_exit

:option_three
echo Chains into another menu...
chain https://raw.githubusercontent.com/${github_user}/netboot.xyz-custom/master/custom1.ipxe || goto custom

:custom_exit
chain utils.ipxe
exit
